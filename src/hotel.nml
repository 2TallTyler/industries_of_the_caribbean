/* Begin Hotel */

spritelayout sprite_hotel_n {
	ground {
		sprite: 1420;
	}
	building {
		sprite: construction_state==0 ? 4471 : construction_state==3 ? 4475 : 4473;
	}
}

spritelayout sprite_hotel_e {
	ground {
		sprite: 1420;
	}
	building {
		sprite: construction_state==0 ? 4472 : construction_state==3 ? 4476 : 4474;
	}
}

item (FEAT_INDUSTRYTILES, tile_hotel_n)
{
	property {
		substitute: 0;
		accepted_cargos: [[PASS, 8]];
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
	graphics {
		default: sprite_hotel_n;
	}
}

item (FEAT_INDUSTRYTILES, tile_hotel_e)
{
	property {
		substitute: 0;
		accepted_cargos: [[PASS, 8]];
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
	graphics {
		default: sprite_hotel_e;
	}
}

tilelayout tilelayout_hotel_square {
	0,0: tile_hotel_n;	0,1: tile_hotel_e;
	1,0: tile_hotel_n;	1,1: tile_hotel_e;
}

/* Boosted production if Rum is supplied */
produce (produce_hotel_boosted,
	[PASS: (incoming_cargo_waiting("PASS") > 200) ? (incoming_cargo_waiting("PASS") / 8) : 3; FOOD: 3; BEER: 3;],	// Consume
	[TOUR: 24;],																								// Produce
	0 																										// Don't run callback again
	)

/* Regular production uses at least 24 passengers/month, but will consume excess passengers (>200) so they don't build up */
produce (produce_hotel,
	[PASS: (incoming_cargo_waiting("PASS") > 200) ? (incoming_cargo_waiting("PASS") / 8) : 3; FOOD: 3;],	// Consume
	[TOUR: 12;],																				// Produce
	0 																							// Don't run callback again
	)

/* Check for Rum for normal or boosted production */
switch (FEAT_INDUSTRIES, SELF, switch_produce_hotel_boosted, incoming_cargo_waiting("BEER") >= 3) {1: produce_hotel_boosted; produce_hotel;}

/* Check for Workers and Food for any production */
switch (FEAT_INDUSTRIES, SELF, switch_produce_hotel, (incoming_cargo_waiting("FOOD") >= 3 && incoming_cargo_waiting("PASS") >= 3)) {1: switch_produce_hotel_boosted; switch_produce_idle;}

item (FEAT_INDUSTRIES, industry_hotel, 14)
{
	property {
		substitute: INDUSTRYTYPE_POWER_PLANT;
		name: string(STR_HOTEL);
		nearby_station_name: string(STR_STATION, string(STR_TOWN), string(STR_HOTEL));
		life_type: IND_LIFE_TYPE_BLACK_HOLE;
		spec_flags: bitmask(IND_FLAG_ONLY_IN_LARGE_TOWNS, IND_FLAG_DO_NOT_FORCE_INSTANCE_AT_MAP_GENERATION);
		layouts: [
			tilelayout_hotel_square,
		];
		cargo_types: [
			accept_cargo("PASS"),
			accept_cargo("FOOD"),
			accept_cargo("BEER"),
			produce_cargo("TOUR", 0),
		];
		prob_map_gen: 1; // FIXME: Balance this later
		prob_in_game: 0;
		fund_cost_multiplier: 2;
		map_colour: 176;
	}
	graphics {
		produce_256_ticks: switch_produce_hotel;
		extra_text_industry: string(STR_HOTEL_HELPTEXT);
	}
}