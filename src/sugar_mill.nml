/* Begin Sugar Mill */

item (FEAT_INDUSTRYTILES, tile_sugar_mill_0)
{
	property {
		substitute: 60;
		override: 60;
		accepted_cargos: [[SGCN, 8], [PASS, 8]];
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, tile_sugar_mill_1)
{
	property {
		substitute: 61;
		override: 61;
		accepted_cargos: [[SGCN, 8], [PASS, 8]];
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, tile_sugar_mill_2)
{
	property {
		substitute: 62;
		override: 62;
		accepted_cargos: [[SGCN, 8], [PASS, 8]];
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, tile_sugar_mill_3)
{
	property {
		substitute: 63;
		override: 63;
		accepted_cargos: [[SGCN, 8], [PASS, 8]];
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

produce (produce_sugar_mill,
	[PASS: 4; SGCN: 4;],	// Consume
	[SUGR: 2; MOLS: 1;],	// Produce
	1 						// Run callback again
	)

/* Consume extra Sugarcane */
produce (produce_sugar_mill_sugarcane_stockpile,
	[SGCN: 1;],               // Consume
	[],                       // Produce
	1                         // Run callback again
	)

/* Don't stockpile too much Sugarcane */
switch (FEAT_INDUSTRIES, SELF, switch_sugar_mill_stockpile, incoming_cargo_waiting("SGCN") > 4096) {
	1: produce_sugar_mill_sugarcane_stockpile;
	produce_none;
}

switch (FEAT_INDUSTRIES, SELF, switch_produce_sugar_mill, (incoming_cargo_waiting("PASS") > 0 && incoming_cargo_waiting("SGCN") >= 4)) {
	1: produce_sugar_mill;
	switch_sugar_mill_stockpile;
}

/* Construction check */
switch (FEAT_INDUSTRIES, SELF, loc_check_sugar_mill, IsBeingFunded() || (!param_primary_only && IsNearTown() && TownHasSecondaryPopulation() && AvoidIndustryType(industry_sugar_mill, 128)))
	{1: return CB_RESULT_LOCATION_ALLOW; return CB_RESULT_LOCATION_DISALLOW;}

item (FEAT_INDUSTRIES, industry_sugar_mill, 4)
{
	property {
		substitute: INDUSTRYTYPE_FOOD_PROCESSING_PLANT;
		name: string(STR_SUGAR_MILL);
		nearby_station_name: string(STR_STATION, string(STR_TOWN), string(STR_SUGAR_MILL));
		life_type: IND_LIFE_TYPE_PROCESSING;
		cargo_types: [
			accept_cargo("PASS"),
			accept_cargo("SGCN"),
			produce_cargo("SUGR", 0),
			produce_cargo("MOLS", 0)
		];
		prob_map_gen: (param_primary_only) ? 0 : 1;
		prob_in_game: 0;
		fund_cost_multiplier: 5;
		map_colour: 15;
	}
	graphics {
		location_check: loc_check_sugar_mill;
		produce_256_ticks: switch_produce_idle;
		produce_cargo_arrival: switch_produce_sugar_mill;
		/* Don't allow any production changes */
		monthly_prod_change: CB_RESULT_IND_PROD_NO_CHANGE;
		random_prod_change: CB_RESULT_IND_PROD_NO_CHANGE;
		extra_text_industry: string(STR_SUGAR_MILL_HELPTEXT);
		extra_text_fund: string(STR_SUGAR_MILL_FUNDTEXT);
		colour: (founder == FOUNDER_GAME) ? COLOUR_WHITE : founder_colour1;
	}
}