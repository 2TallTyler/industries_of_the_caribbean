
/* Begin Coffee Plantation */

spritelayout coffee_plantation_office_1 {
	ground {
		sprite: 2116;
	}
	building {
		sprite: 4450;
	}
}

spritelayout coffee_plantation_office_2 {
	ground {
		sprite: 2116;
	}
	building {
		sprite: 4456;
	}
}

random_switch (FEAT_INDUSTRYTILES, SELF, random_coffee_plantation_office) {
	1: return coffee_plantation_office_1;
	1: return coffee_plantation_office_2;
}

item (FEAT_INDUSTRYTILES, ind_tile_coffee_plantation) {
	property {
		substitute: 117;
		override: 117;
		land_shape_flags: bitmask(LSF_ONLY_ON_FLAT_LAND);
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_coffee_plantation_office) {
	property {
		substitute: 117;
		land_shape_flags: bitmask(LSF_ONLY_ON_FLAT_LAND);
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
	graphics {
		default: random_coffee_plantation_office;
	}
}

tilelayout tilelayout_coffee_plantation_1 {
	0,0: ind_tile_coffee_plantation;	0,1: ind_tile_coffee_plantation;	0,2: ind_tile_coffee_plantation;	0,3: ind_tile_coffee_plantation;
	1,0: ind_tile_coffee_plantation;	1,1: ind_tile_coffee_plantation;	1,2: ind_tile_coffee_plantation;	1,3: ind_tile_coffee_plantation;
	2,0: ind_tile_coffee_plantation;	2,1: ind_tile_coffee_plantation;	2,2: ind_tile_coffee_plantation;	2,3: ind_tile_coffee_plantation;
										3,1: ind_tile_coffee_plantation_office;
}

tilelayout tilelayout_coffee_plantation_2 {
	0,0: ind_tile_coffee_plantation;	0,1: ind_tile_coffee_plantation;	0,2: ind_tile_coffee_plantation;	0,3: ind_tile_coffee_plantation;
	1,0: ind_tile_coffee_plantation;	1,1: ind_tile_coffee_plantation;	1,2: ind_tile_coffee_plantation;	1,3: ind_tile_coffee_plantation;	1,4: ind_tile_coffee_plantation_office;
	2,0: ind_tile_coffee_plantation;	2,1: ind_tile_coffee_plantation;	2,2: ind_tile_coffee_plantation;	2,3: ind_tile_coffee_plantation;
}

tilelayout tilelayout_coffee_plantation_3 {
	0,0: ind_tile_coffee_plantation;	0,1: ind_tile_coffee_plantation;	0,2: ind_tile_coffee_plantation;	0,3: ind_tile_coffee_plantation_office;
	1,0: ind_tile_coffee_plantation;	1,1: ind_tile_coffee_plantation;	1,2: ind_tile_coffee_plantation;
	2,0: ind_tile_coffee_plantation;	2,1: ind_tile_coffee_plantation;	2,2: ind_tile_coffee_plantation;
	3,0: ind_tile_coffee_plantation;	3,1: ind_tile_coffee_plantation;	3,2: ind_tile_coffee_plantation;
}

tilelayout tilelayout_coffee_plantation_4 {
	0,0: ind_tile_coffee_plantation;	0,1: ind_tile_coffee_plantation;	0,2: ind_tile_coffee_plantation;
	1,0: ind_tile_coffee_plantation;	1,1: ind_tile_coffee_plantation;	1,2: ind_tile_coffee_plantation;
	2,0: ind_tile_coffee_plantation;	2,1: ind_tile_coffee_plantation;	2,2: ind_tile_coffee_plantation;
	3,0: ind_tile_coffee_plantation;	3,1: ind_tile_coffee_plantation;	3,2: ind_tile_coffee_plantation;	3,3: ind_tile_coffee_plantation_office;
}

/* Coffee plantations must be at least height 2, no max limit. */
switch (FEAT_INDUSTRIES, SELF, loc_check_coffee_plantation, IsNotDesert() && IndustryElevationCheck(2, 255)) {1: return CB_RESULT_LOCATION_ALLOW; return string(STR_ERR_COFFEE_LOCATION);}

/* Boosted production if Fertilizier is supplied */
produce (produce_coffee_plantation_boosted,
	[PASS: (incoming_cargo_waiting("PASS") > 200) ? (incoming_cargo_waiting("PASS") / 8) : 3; FERT: 3;],	// Consume
	[JAVA: 24;],																								// Produce
	0 																										// Don't run callback again
	)

/* Regular production uses at least 24 passengers/month, but will consume excess passengers (>200) so they don't build up */
produce (produce_coffee_plantation,
	[PASS: (incoming_cargo_waiting("PASS") > 200) ? (incoming_cargo_waiting("PASS") / 8) : 3;],	// Consume
	[JAVA: 12;],																					// Produce
	0 																							// Don't run callback again
	)

/* Check for Fertilizier for normal or boosted production */
switch (FEAT_INDUSTRIES, SELF, switch_produce_coffee_plantation_boosted, incoming_cargo_waiting("FERT") >= 3) {1: produce_coffee_plantation_boosted; produce_coffee_plantation;}

/* Check for Workers for any production */
switch (FEAT_INDUSTRIES, SELF, switch_produce_coffee_plantation, incoming_cargo_waiting("PASS") >= 3) {1: switch_produce_coffee_plantation_boosted; produce_none;}

item (FEAT_INDUSTRIES, industry_coffee_plantation, 0)
{
	property {
		substitute: INDUSTRYTYPE_RUBBER_PLANTATION;
		name: string(STR_COFFEE_PLANTATION);
		nearby_station_name: string(STR_STATION, string(STR_TOWN), string(STR_COFFEE_PLANTATION));
		life_type: IND_LIFE_TYPE_ORGANIC;
		cargo_types: [
			accept_cargo("PASS"),
			accept_cargo("FERT"),
			produce_cargo("JAVA", 0),
		];
		layouts: [
			tilelayout_coffee_plantation_1,
			tilelayout_coffee_plantation_2,
			tilelayout_coffee_plantation_3,
			tilelayout_coffee_plantation_4
		];
		conflicting_ind_types: [industry_coffee_plantation, industry_sugarcane_plantation]; // FIXME: Add plantations which plant fields around them
		prob_map_gen: 10; // FIXME: Adjust as necessary
		prob_in_game: 0;
		map_colour: 63;
		prospect_chance: 1;
		
	}
	graphics {
		produce_256_ticks: switch_produce_coffee_plantation;
		location_check: loc_check_coffee_plantation;
		/* Don't allow any production changes */
		build_prod_change: 16; // 100% production
		monthly_prod_change: CB_RESULT_IND_PROD_NO_CHANGE;
		random_prod_change: CB_RESULT_IND_PROD_NO_CHANGE;
		extra_text_industry: string(STR_PRIMARY_HELPTEXT);
	}
}
