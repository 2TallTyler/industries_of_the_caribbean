
/* Begin Import/Export */

spritelayout sprlay_import_export_buoy {
	ground {
		sprite: 4061;
	}
	building {
		sprite: 4790;
	}
}

item (FEAT_INDUSTRYTILES, ind_tile_import_export_buoy) {
	property {
		substitute: 0;
		land_shape_flags: bitmask(LSF_ALLOW_ON_WATER);
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
	graphics {
		default: sprlay_import_export_buoy;
	}
}

tilelayout tilelayout_import_export {
	0,0: 24;
	0,1: 24;
	1,0: 24;
	1,1: ind_tile_import_export_buoy;
}

switch (FEAT_INDUSTRIES, SELF, loc_check_import_export_0,
	IsNearMapEdge() && 
	((DistanceFromWaterOrLand(8) && industry_distance(industry_import_export_0) > 512) || IsFundedConstruction())
) {1: return CB_RESULT_LOCATION_ALLOW; return string(STR_ERR_IMPORT_EXPORT_LOCATION);}

item (FEAT_INDUSTRIES, industry_import_export_0, 8) {
	property {
		substitute: 9;
		name: string(STR_IMPORT_EXPORT);
		nearby_station_name: string(STR_STATION, string(STR_TOWN), string(STR_IMPORT_EXPORT));
		life_type: IND_LIFE_TYPE_BLACK_HOLE;
		spec_flags: bitmask(IND_FLAG_BUILT_ON_WATER, IND_FLAG_AI_CREATES_AIR_AND_SHIP_ROUTES);
		cargo_types: [
			accept_cargo("JAVA", produce_cargo("FOOD", 1)),
			accept_cargo("SUGR", produce_cargo("OIL_", 1)), // FIXME: Why doesn't Oil show up on the industry window?
			accept_cargo("NICK", produce_cargo("FERT", 1)),
			accept_cargo("COBL", produce_cargo("PIPE", 1)),
			accept_cargo("CIGR", produce_cargo("RFPR", 1)),
			accept_cargo("TOUR", produce_cargo("ENSP", 1)),
		];
		layouts: [
			tilelayout_import_export
		];
		prob_map_gen: 1; // FIXME: Adjust as necessary
		prob_in_game: 0;
		map_colour: 15;
		prospect_chance: 1;
		fund_cost_multiplier: 0;
	}
	graphics {
		location_check: loc_check_import_export_0;
	}
}
